group "com.manerfan.translator"
version "1.0.0"
description "document translator"

buildscript {
    ext {
        kotlin_version = "1.2.10"
        springboot_version = "2.0.+"
        cglib_version = "3.2.+"
    }

    repositories {
        maven {
            name "aliyun maven central"
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
        maven {
            name "spring repo milestone"
            url "https://repo.spring.io/milestone"
        }
        maven {
            name "spring plugins"
            url "http://repo.spring.io/plugins-release"
        }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springboot_version"
    }
}

apply plugin: "idea"

idea {
    project {
        languageLevel = "1.8"
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

configure(subprojects - project(":translator-ui")) {
    apply plugin: "java"
    apply plugin: "java-library"

    apply plugin: "kotlin"
    apply plugin: "kotlin-allopen"
    apply plugin: "kotlin-spring"

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springboot_version"
        }
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        mavenLocal()
        maven {
            name 'aliyun maven central'
            url 'http://maven.aliyun.com/nexus/content/groups/public'
        }
        maven {
            name 'spring repo milestone'
            url 'https://repo.spring.io/milestone'
        }
        maven {
            name 'EBI'
            url 'https://www.ebi.ac.uk/intact/maven/nexus/content/repositories/ebi-repo/'
            /* for cpdetector */
        }
        mavenCentral()
        jcenter()
    }

    configurations {
        all*.exclude module: 'spring-boot-starter-tomcat'
        all*.exclude module: 'spring-boot-starter-logging'
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-devtools'

        compile 'org.springframework.boot:spring-boot-starter-log4j2'

        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.20"

        compile 'com.google.guava:guava:23.2-jre'
        compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.2"

        testCompile "org.springframework.boot:spring-boot-starter-test"
        testCompile "junit:junit:5.+"
    }
}
